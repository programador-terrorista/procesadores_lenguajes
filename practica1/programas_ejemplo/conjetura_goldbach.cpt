------------------------------------------------------------------------
--  conjetura_goldbach.cpt
------------------------------------------------------------------------
proc main is
------------------------------------------------------------------------
    var n, i: int;
------------------------------------------------------------------------
    func pedir_entero() -> int is
        var n: int;

        n = 0;
        loop while ((n <= 2) or (n mod 2 <> 0)) do
            print("Dame un numero PAR mayor que 2: ");
            read(n);
        done
        return n;
    done
------------------------------------------------------------------------
    func es_primo(in k: int) -> bool is
        var i: int;
        var primo: bool;

        i = 2;
        primo = true;
        loop while ((i <= (k / 2)) and primo) do
            primo = (k mod i <> 0);
            i = i + 1;
        done
        return primo;
    done
------------------------------------------------------------------------
    func hay_un_par(in k: int) -> bool is
        var i, n, a, b: int;

        a = 2;
        b = 2;
        n = 0;
        print(k, " es la suma de: ");

        loop while ((a < k) and (b < k)) do
            when (es_primo(a) and es_primo(b) and ((a + b) == k)) do
                print(a, "+", b, ", ");
                n = n + 1;
            done

            a = a + 1;

            when (a == k) do
                b = b + 1;
                a = b;
            done
        done

        println(n, " parejas de primos en total.");

        return (n > 0);
    done
------------------------------------------------------------------------
    println("Conjetura de Golbach: todo número par mayor que 2 puede escribirse como suma de dos números primos.");
    println();

    println("Este programa comprueba si es cierta hasta el número que le digas.");
    println();

    n = pedir_entero();
    i = 4;

    loop while (i <= n) do
        when (not hay_un_par(i)) do
            println("La conjetura no se cumple para ", i, "! (Seguro que por error del compilador!)");
        done
        i = i + 2;
    done
done
